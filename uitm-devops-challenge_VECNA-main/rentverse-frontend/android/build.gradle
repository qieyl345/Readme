// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.2'
        classpath 'com.google.gms:google-services:4.4.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply from: "variables.gradle"

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects {
    configurations.all {
        resolutionStrategy {
            eachDependency { DependencyResolveDetails details ->
                // Force all Kotlin dependencies to use the same version
                if (details.requested.group == 'org.jetbrains.kotlin') {
                    details.useVersion '1.8.22'
                }
                // Replace kotlin-stdlib-jdk7 and kotlin-stdlib-jdk8 with kotlin-stdlib
                if (details.requested.group == 'org.jetbrains.kotlin' &&
                    (details.requested.name == 'kotlin-stdlib-jdk7' || details.requested.name == 'kotlin-stdlib-jdk8')) {
                    details.useTarget 'org.jetbrains.kotlin:kotlin-stdlib:1.8.22'
                }
            }
        }
    }
}

// Ensure all Android subprojects use Java 17
subprojects {
    afterEvaluate { project ->
        if (project.plugins.hasPlugin('com.android.application') || project.plugins.hasPlugin('com.android.library')) {
            project.android.compileOptions {
                sourceCompatibility JavaVersion.VERSION_17
                targetCompatibility JavaVersion.VERSION_17
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
